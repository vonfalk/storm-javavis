use core:lang;
use lang:bs;
use lang:bs:macro;

// Logic to generate unique query ID:s that we can later use to cache prepared statements for
// databases. That way, we can instruct the database about our intentions and don't have to rely as
// much on caches in the database.
private Nat nextQueryID on Compiler;

// Generate a connection ID for a particular
private Nat newQueryID() on Compiler {
	return nextQueryID++;
}

private Expr bindParam(SrcPos pos, Block block, Expr statement, Nat id, Expr param) on Compiler {
	if (isMaybe(param.result.type)) {
		WeakMaybeCast cast(param);
		cast.name(SStr("x"));
		If check(block, cast);

		CondSuccess success(pos, block, cast);
		check.trueCode = success;
		if (created = cast.result) {
			Actuals params;
			params.add(NumLiteral(pos, id));
			params.add(LocalVarAccess(pos, created));
			success.set(namedExpr(block, pos, "bind", statement, params));
		}

		check.falseCode = namedExpr(block, pos, "bindNull", statement, Actuals(NumLiteral(pos, id)));

		check;
	} else {
		Actuals params;
		params.add(NumLiteral(pos, id));
		params.add(param);
		namedExpr(block, pos, "bind", statement, params);
	}
}

// Create a query expression for a typed query. Helper.
private Expr typedQueryExpr(SrcPos pos, Block block, Expr connection, Database contents, Query query) on Compiler {
	query.resolve(ResolveContext(block, contents));
	var q = query.build();

	ExprBlock r(pos, block);

	// Query ID.
	Nat id = newQueryID();

	// Create a prepared statement.
	var prepared = {
		// Call "connection.prepare(id, query)".
		Actuals params;
		params.add(NumLiteral(pos, id));
		params.add(StrLiteral(pos, q.query.toS));
		var stmt = namedExpr(r, pos, "prepare", connection, params);
		var varDecl = Var(r, SStr("statement"), stmt);
		r.add(varDecl);
		LocalVarAccess(pos, varDecl.var);
	};

	// Bind parameters.
	for (i, x in q.bind) {
		r.add(bindParam(pos, r, prepared, i, x));
	}

	// Execute it!
	r.add(namedExpr(r, pos, "execute", prepared));

	// Get the result, if desired.
	if (e = query.result(r, prepared))
		r.add(e);

	r;
}

// Create a query expression for an untyped query. Helper.
private Expr untypedQueryExpr(SrcPos pos, Block block, Expr database, Query query) on Compiler {
	query.resolve(ResolveContext(block));
	var q = query.build();

	ExprBlock r(pos, block);

	var prepared = {
		var stmt = namedExpr(r, pos, "prepare", database, Actuals(StrLiteral(pos, q.query.toS)));
		var varDecl = Var(r, SStr("statement"), stmt);
		r.add(varDecl);
		LocalVarAccess(pos, varDecl.var);
	};

	// Bind parameters.
	for (i, x in q.bind) {
		Actuals params;
		params.add(NumLiteral(pos, i));
		params.add(x);
		r.add(namedExpr(r, pos, "bind", prepared, params));
	}

	// Execute it!
	r.add(namedExpr(r, pos, "execute", prepared));

	// Get the result, if desired. Otherwise, finalize the query.
	if (e = query.result(r, prepared))
		r.add(e);
	else
		r.add(namedExpr(r, pos, "finalize", prepared));

	r;
}


// Create a suitable query expression depending on what database connection was used.
Expr queryExpr(SrcPos pos, Block block, Expr expr, Query query) on Compiler {
	var result = expr.result().type();
	if (x = result.type as DatabaseType) {
		// Typed version.
		return typedQueryExpr(pos, block, expr, x.contents, query);
	} else if (Value(named{DBConnection}).canStore(result)) {
		// Untyped version.
		return untypedQueryExpr(pos, block, expr, query);
	} else {
		throw SyntaxError(expr.pos, "Expected a database connection (typed or untyped).");
	}
}

// A Query block.
class QueryBlock extends ExprBlock {
	init(SrcPos pos, Block parent, Expr db) {
		var result = db.result.type;
		if (Value(named{DBConnection}).canStore(result)) {
		} else if (result.type as DatabaseType) {
		} else {
			throw SyntaxError(db.pos, "Expected a database connection (typed or untyped).");
		}

		super(pos, parent);

		Var var(this, SStr(" db", db.pos), db);
		add(var);

		init {
			connection(db.pos, var.var);
		}
	}

	// Variable containing our database connection.
	LocalVarAccess connection;

	// Helper to find a Query block.
	QueryBlock find(Block in) : static {
		NameLookup? at = in.lookup;
		while (l = at as BlockLookup) {
			if (q = l.block as QueryBlock)
				return q;

			at = l.parent;
		}

		throw InternalError("Could not find a parent QueryBlock!");
	}
}

// Create a suitable query expression, assuming we're inside a WITH block.
Expr queryBlockExpr(SrcPos pos, Block block, Query query) on Compiler {
	QueryBlock q = QueryBlock:find(block);
	queryExpr(pos, block, q.connection, query);
}

/**
 * Context passed around while resolving a SQL query.
 */
class ResolveContext on Compiler {
	// Current BS block.
	Block block;

	// Contents of the database.
	Database? db;

	// Which tables are visible in the current context?
	Table[] visible;

	// Create typed version.
	init(Block block, Database db) {
		init { block = block; db = db; }
	}

	// Create untyped version.
	init(Block block) {
		init { block = block; }
	}

	// Is this a typed query?
	Bool typed() {
		db.any;
	}

	// Add a table to 'current'. Throws if the table is known not to exists.
	Table? addTable(SrcPos pos, Str to) {
		// Untyped version?
		unless (db)
			return null;

		unless (found = db.find(to))
			throw NoSuchTable(pos, to);
		visible << found;
		found;
	}

	// Result from "resolve"
	value ResolveResult {
		Str table;
		Column column;

		init(Str table, Column column) {
			init { table = table; column = column; }
		}
	}

	// Resolve a column.
	ResolveResult? resolve(SrcPos pos, Str? table, Str column) {

		if (table) {
			// TODO: Hashtable. That allows aliases as well.
			for (t in visible) {
				if (t.name != table)
					continue;

				if (col = t.find(column))
					return ResolveResult(t.name, col);
			}
			return null;
		} else {
			ResolveResult? result;
			for (t in visible) {
				if (col = t.find(column)) {
					if (result) {
						throw SyntaxError(pos, "The column name ${column} is ambigous. It exists in tables ${result.table} and ${t.name} at least.");
					} else {
						result = ResolveResult(t.name, col);
					}
				}
			}
			return result;
		}
	}
}


/**
 * Query builder. Collects a SQL query string in a StrBuf, as well as BS expressions for any
 * parameters that need to be bound.
 */
class QueryBuilder on Compiler {
	// String builder that contains the final SQL query.
	StrBuf query;

	// Expressions that are used to bind parameters.
	Expr[] bind;

	// Append a properly escaped SQL name to the query.
	void name(Str name) {
		// It seems in standard SQL, double quotes are used for identifiers.
		// I am unable to find anything that describes how to include double quotes in an identifier.
		// Perhaps we should just disallow that (currently it is implicitly disallowed due to the grammar).
		query << "\"" << name << "\"";
	}

	// To string.
	void toS(StrBuf to) : override {
		to << "Query: " << query.toS;
		to << "\nData: " << bind;
	}
}


/**
 * Base class for an SQL query.
 *
 * Note: .toS() looks like SQL, but does not take proper care of edge-cases, so should not be
 * treated as a proper SQL statement.
 */
class Query on Compiler {
	// Position in source.
	SrcPos pos;

	// Create.
	init(SrcPos pos) {
		init() {
			pos = pos;
		}
	}

	// Resolve any unknowns in this query, given a suitable context.
	// Also typechecks as applicable.
	void resolve(ResolveContext context) : abstract;

	// Build a query.
	QueryBuilder build() : abstract;

	// Compute the result of this query, if it should be available.
	Expr? result(Block context, Expr statement) {
		null;
	}
}

/**
 * INSERT query.
 */
class InsertQuery extends Query {
	// Table name.
	SStr table;

	// Columns to insert into. If empty, we insert into all columns.
	SStr[] columns;

	// Values to insert.
	SQLExpr[] values;

	// Create, insert all columns.
	init(SrcPos pos, SStr table, SQLExpr[] values) {
		init(pos) {
			table = table;
			values = values;
		}
	}

	// Create, insert into only a subset of columns.
	init(SrcPos pos, SStr table, SStr[] columns, SQLExpr[] values) {
		init(pos) {
			table = table;
			columns = columns;
			values = values;
		}
	}

	// Resolve.
	void resolve(ResolveContext context) : override {
		var table = context.addTable(table.pos, table.v);

		// If we found a table, explicitly specify the column names for easier typechecking later.
		Column[] cols;
		if (table) {
			if (columns.empty) {
				for (c in table.columns)
					columns << SStr(c.name, pos);
			} else {
				// Find all of the columns.
				Set<Str> used;
				for (c in columns) {
					used.put(c.v);
					unless (found = table.find(c.v))
						throw NoSuchColumn(c.pos, c.v, table.name);
					cols << found;
				}

				Bool multiPK = table.multiplePK();

				// Check the ones that are not inserted.
				for (c in table.columns) {
					if (used.has(c.name))
						continue;

					if (!c.hasDefault(multiPK))
						throw SyntaxError(pos, "The column ${c.name} has no default value, and needs to be given a value.");
				}
			}
		}

		if (cols.any & (values.count != cols.count))
			throw SyntaxError(pos, "The number of values does not match the number of columns inserted into.");

		// It does not really make sense to refer to column names in an insert statement...
		context.visible.clear();

		for (Nat i = 0; i < values.count; i++)
			values[i] = values[i].resolve(context);

		for (i, col in cols) {
			Value result(values[i].result);
			if (col.allowNull)
				result = unwrapMaybe(result);

			if (!Value(col.datatype.storm).canStore(result))
				throw SyntaxError(values[i].pos, "Can not store a value of type ${result} in the column \"${col.name}\".");
		}
	}

	// Build the query.
	QueryBuilder build() : override {
		QueryBuilder r;

		r.query << "INSERT INTO ";
		r.name(table.v);
		if (columns.any) {
			r.query << " (";
			for (i, col in columns) {
				if (i > 0)
					r.query << ", ";
				r.name(col.v);
			}
			r.query << ")";
		}
		r.query << " VALUES (";
		for (i, v in values) {
			if (i > 0)
				r.query << ", ";
			v.build(r);
		}
		r.query << ");";

		r;
	}

	// Return the last created row ID.
	Expr? result(Block context, Expr statement) {
		namedExpr(context, pos, "lastRowId", statement);
	}

	// To string.
	void toS(StrBuf to) : override {
		to << "INSERT INTO " << table.v;
		if (columns.any)
			to << " (" << join(columns, ", ", (x) => x.v) << ")";

		to << " VALUES (" << join(values, ", ") << ");";
	}
}

// Helper to check that a condition returns a boolean. Assumes that the condition was resolved beforehand.
private void checkCondition(SQLExpr condition) on Compiler {
	var result = condition.result;
	if (!Value(named{Bool}).canStore(unwrapMaybe(result)))
		throw SyntaxError(condition.pos, "Expressions in WHERE and ON clauses are expected to return a Bool, not ${result}.");
}

/**
 * UPDATE query.
 */
class UpdateQuery extends Query {
	// Table to update.
	SStr table;

	// Columns to update.
	AssignExpr[] update;

	// Condition, if any.
	SQLExpr? condition;

	// Create.
	init(SrcPos pos, SStr table, AssignExpr[] update, SQLExpr? condition) {
		init(pos) {
			table = table;
			update = update;
			condition = condition;
		}
	}

	// Resolve.
	void resolve(ResolveContext context) : override {
		var table = context.addTable(table.pos, table.v);

		// Resolve all assignments.
		for (x in update) {
			x.value = x.value.resolve(context);
		}

		// Update the expression if suitable.
		if (x = condition) {
			var resolved = x.resolve(context);
			if (context.typed)
				checkCondition(resolved);
			condition = resolved;
		}

		if (table) {
			// Check if all columns exist, and type-check the assignments.
			for (x in update) {
				unless (column = table.find(x.column.v))
					throw NoSuchColumn(x.column.pos, x.column.v, table.name);

				Value result(x.value.result);
				if (!Value(column.datatype.storm).canStore(result))
					throw SyntaxError(x.value.pos, "Cannot assign a ${result} to column ${column.name}");
			}
		}
	}

	// Build the query.
	QueryBuilder build() : override {
		QueryBuilder r;

		r.query << "UPDATE ";
		r.name(table.v);
		r.query << " SET ";
		for (i, x in update) {
			if (i > 0)
				r.query << ", ";
			r.query << x.column.v << " = ";
			x.value.build(r);
		}

		if (condition) {
			r.query << " WHERE ";
			condition.build(r);
		}

		r.query << ";";

		r;
	}

	// Return the number of modified rows.
	Expr? result(Block context, Expr statement) {
		namedExpr(context, pos, "changes", statement);
	}

	// To string.
	void toS(StrBuf to) : override {
		to << "UPDATE " << table.v << " SET " << join(update, ", ");
		if (condition) {
			to << " WHERE " << condition;
		}
	}
}

/**
 * Value to update in an UPDATE query.
 */
class AssignExpr on Compiler {
	// Column to update.
	SStr column;

	// Value to assign.
	SQLExpr value;

	// Create.
	init(SStr column, SQLExpr value) {
		init { column = column; value = value; }
	}

	// To string.
	void toS(StrBuf to) : override {
		to << column.v << " = " << value;
	}
}


/**
 * DELETE query.
 */
class DeleteQuery extends Query {
	// Table to update.
	SStr table;

	// Condition, if any.
	SQLExpr? condition;

	// Create.
	init(SrcPos pos, SStr table, SQLExpr? condition) {
		init(pos) {
			table = table;
			condition = condition;
		}
	}

	// Resolve.
	void resolve(ResolveContext context) : override {
		context.addTable(table.pos, table.v);

		if (x = condition) {
			var resolved = x.resolve(context);
			if (context.typed)
				checkCondition(resolved);
			condition = resolved;
		}
	}

	// Build the query.
	QueryBuilder build() : override {
		QueryBuilder r;

		r.query << "DELETE FROM ";
		r.name(table.v);
		if (condition) {
			r.query << " WHERE ";
			condition.build(r);
		}
		r.query << ";";

		r;
	}

	// Return the number of modified rows.
	Expr? result(Block context, Expr statement) {
		namedExpr(context, pos, "changes", statement);
	}

	// To string.
	void toS(StrBuf to) : override {
		to << "DELETE FROM " << table.v;
		if (condition) {
			to << " WHERE " << condition;
		}
	}
}


/**
 * SELECT query.
 */
class SelectQuery extends Query {
	// Table.
	SStr table;

	/**
	 * Column to extract.
	 */
	class Column on Compiler {
		// Position
		SrcPos pos;

		// Table (optional).
		Str? table;

		// Column.
		Str column;

		// Optional alternate name.
		Str as;

		// Type of this column, if available.
		Type? type;

		init(SrcPos pos, SStr column) {
			init {
				pos = pos;
				column = column.v;
				as = column.v;
			}
		}

		init(SrcPos pos, SStr table, SStr column) {
			init {
				pos = pos;
				table = table.v;
				column = column.v;
				as = table.v # "_" # column.v;
			}
		}

		init(SrcPos pos, Str? table, Str column) {
			init {
				pos = pos;
				table = table;
				column = column;
				as = column;
			}

			if (table)
				as = table # "_" # column;
		}

		void setAs(SStr to) {
			as = to.v;
		}

		// Build.
		void build(QueryBuilder to) {
			if (table) {
				to.name(table);
				to.query << ".";
			}
			to.name(column);
		}

		// ToS.
		void toS(StrBuf to) : override {
			if (table)
				to << table << ".";
			to << column << " AS " << as;
		}
	}

	// Columns to select. If empty, we assume all columns.
	Column[] cols;

	/**
	 * Join statement.
	 */
	class Join on Compiler {
		// Type of join.
		Str type;

		// Table.
		SStr table;

		// Condition for join.
		SQLExpr expr;

		// Create.
		init(Str type, SStr table, SQLExpr expr) {
			init {
				type = type;
				table = table;
				expr = expr;
			}
		}

		// Build.
		void build(QueryBuilder to) {
			to.query << type << " ";
			to.name(table.v);
			to.query << " ON ";
			expr.build(to);
		}

		// ToS.
		void toS(StrBuf to) : override {
			to << type << " " << table.v << " ON " << expr;
		}
	}

	// Join clauses.
	Join[] joins;

	// Where clause, if present.
	SQLExpr? condition;

	init(SrcPos pos, SStr table, Column[] cols, Join[] joins, SQLExpr? condition) {
		init(pos) {
			table = table;
			cols = cols;
			joins = joins;
			condition = condition;
		}
	}

	// Resolve.
	void resolve(ResolveContext context) : override {
		context.addTable(table.pos, table.v);

		for (j in joins) {
			context.addTable(j.table.pos, j.table.v);
			// TODO: Might be too early, but I think this is how SQL works.
			j.expr = j.expr.resolve(context);
			if (context.typed)
				checkCondition(j.expr);
		}

		if (context.visible.any)
			resolveOutput(context);

		if (x = condition) {
			var resolved = x.resolve(context);
			if (context.typed)
				checkCondition(resolved);
			condition = resolved;
		}
	}

	// Resolve the output data.
	private void resolveOutput(ResolveContext context) {
		if (cols.empty) {
			// No columns -> we need to add all visible columns.
			for (table in context.visible) {
				for (col in table.columns) {
					Column c = if (joins.empty) {
						Column(pos, null, col.name);
					} else {
						Column(pos, table.name, col.name);
					};
					c.type = resultType(col);
					cols << c;
				}
			}
		} else {
			// We have some columns. Check them.
			for (col in cols) {
				unless (result = context.resolve(col.pos, col.table, col.column)) {
					if (table = col.table)
						throw NoSuchColumn(col.pos, col.column, table);
					else
						throw NoSuchColumn(col.pos, col.column, table.v);
				}

				col.type = resultType(result.column);
			}
		}
	}

	// Figure out the type for a column.
	private Type resultType(sql:Column col) {
		Type t = col.datatype.storm;
		if (col.allowNull) {
			if (maybe = wrapMaybe(t).type)
				return maybe;
		}
		t;
	}

	// Build the query.
	QueryBuilder build() : override {
		QueryBuilder r;

		r.query << "SELECT ";
		if (cols.empty) {
			r.query << "*";
		} else {
			for (i, name in cols) {
				if (i > 0)
					r.query << ", ";
				name.build(r);
			}
		}
		r.query << " FROM ";
		r.name(table.v);

		for (j in joins) {
			r.query << " ";
			j.build(r);
		}

		if (condition) {
			r.query << " WHERE ";
			condition.build(r);
		}

		r.query << ";";
		r;
	}

	// Return a proper iterator.
	Expr? result(Block context, Expr statement) {
		Bool noTypeInfo = cols.empty;

		ValParam[] types;
		if (!noTypeInfo) {
			for (c in cols) {
				if (t = c.type)
					types << ValParam(t, c.as);
				else
					noTypeInfo = true;
			}
		}

		// If we don't have type info, just return a plain iterator.
		if (noTypeInfo)
			return namedExpr(context, pos, "iter", statement);

		var iterType = getTypedIter(types);
		unless (ctor = iterType.find("__init", [Value(iterType), statement.result.type.asRef(false)], Scope()) as Function)
			throw InternalError("Could not find a suitable constructor in the generated type.");

		CtorCall(pos, context.scope, ctor, Actuals(statement));
	}

	// To string.
	void toS(StrBuf to) : override {
		to << "SELECT " << join(cols, ", ") << " FROM " << table.v;
		for (j in joins)
			to << join;
		if (condition)
			to << " WHERE " << condition;
	}
}

/**
 * SELECT ONE query.
 *
 * Works just like SELECT, but returns Maybe<Row> rather than an iterator.
 */
class SelectOneQuery extends SelectQuery {
	init(SrcPos pos, SStr table, Column[] cols, Join[] joins, SQLExpr? condition) {
		init(pos, table, cols, joins, condition) {}
	}

	// Modified result. Returns a single row (wrapped in Maybe) rather than an iterator.
	Expr? result(Block context, Expr statement) {
		Bool noTypeInfo = cols.empty;

		ValParam[] types;
		if (!noTypeInfo) {
			for (c in cols) {
				if (t = c.type)
					types << ValParam(t, c.as);
				else
					noTypeInfo = true;
			}
		}

		// No type info - just return a Maybe<Row> object.
		if (noTypeInfo)
			return namedExpr(context, pos, "fetch", statement);

		// Otherwise, we want to return either <null> or an instance of the row-type.
		TypedRow t = getTypedRow(types);
		unless (ctor = t.find("__init", [Value(t), Value(named{Row})], Scope()) as Function)
			throw InternalError("Could not find a suitable constructor in the generated type.");

		unless (maybe = wrapMaybe(Value(t)).type)
			throw InternalError("Could not find the maybe type for the generated row type.");

		// Create an if-statement.
		WeakMaybeCast cast(namedExpr(context, pos, "fetch", statement));
		cast.name(SStr("x"));
		If check(context, cast);

		CondSuccess success(pos, context, cast);
		check.trueCode = success;
		if (created = cast.result) {
			success.set(CtorCall(pos, Scope(), ctor, Actuals(LocalVarAccess(pos, created))));
		}

		if (maybeCtor = maybe.find("__init", [Value(maybe, true)], Scope()) as Function) {
			check.falseCode = CtorCall(pos, Scope(), maybeCtor, Actuals());
		}

		check;
	}
}

/**
 * CREATE TABLE query.
 *
 * Only for untyped connections.
 */
class CreateQuery extends Query {
	// Table declaration.
	Table table;

	// Is this a "CREATE IF NOT EXISTS"?
	Bool ifNotExists;

	// Create.
	init(SrcPos pos, Bool ifNotExists, Table table) {
		init(pos) {
			table = table;
			ifNotExists = ifNotExists;
		}
	}

	// Resolve.
	void resolve(ResolveContext context) : override {
		if (context.typed)
			throw SyntaxError(pos, "Can not use CREATE TABLE queries with typed connections.");
	}

	// Build the query.
	QueryBuilder build() : override {
		QueryBuilder r;
		r.query << table.toSQL(ifNotExists);
		r;
	}

}

/**
 * CREATE INDEX query.
 *
 * Only for untyped connections.
 */
class IndexQuery extends Query {
	// Index name.
	Str name;

	// Table name.
	SStr table;

	// Columns.
	SStr[] columns;

	// Create.
	init(SrcPos pos, SStr name, SStr table, SStr[] columns) {
		init(pos) {
			name = name.v;
			table = table;
			columns = columns;
		}
	}

	// Create.
	init(SrcPos pos, SStr table, SStr[] columns) {
		StrBuf name;
		name << table.v << "_" << join(columns, "_", (x) => x.v);

		init(pos) {
			name = name.toS;
			table = table;
			columns = columns;
		}
	}

	// Resolve.
	void resolve(ResolveContext context) : override {
		if (context.typed)
			throw SyntaxError(pos, "Can not use CREATE TABLE queries with typed connections.");
	}

	// Build the query.
	QueryBuilder build() : override {
		QueryBuilder r;

		r.query << "CREATE INDEX ";
		r.name(name);
		r.query << " ON ";
		r.name(table.v);
		r.query << "(";
		for (i, c in columns) {
			if (i > 0)
				r.query << ", ";
			r.name(c.v);
		}
		r.query << ");";

		r;
	}

}

/**
 * DROP TABLE query.
 *
 * Only for untyped connections.
 */
class DropQuery extends Query {
	// Table name.
	SStr table;

	// Create.
	init(SrcPos pos, SStr table) {
		init(pos) {
			table = table;
		}
	}

	// Resolve.
	void resolve(ResolveContext context) : override {
		if (context.typed)
			throw SyntaxError(pos, "Can not use CREATE TABLE queries with typed connections.");
	}

	// Build the query.
	QueryBuilder build() : override {
		QueryBuilder r;

		r.query << "DROP TABLE ";
		r.name(table.v);
		r.query << ";";

		r;
	}

}
