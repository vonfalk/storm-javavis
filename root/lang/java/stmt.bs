use core:lang;
use core:asm;
use lang:bs:macro;

class Stmt on Compiler {
    SrcPos pos;
    
    init(SrcPos pos) {
        init { pos = pos; }
    }

    Bool code(CodeGen gen) : abstract;
}

package Bool executeStmt(Block inside, CodeGen gen, Stmt s) on Compiler {
    if (s as Block)
        s.code(gen);
    else {
        //if (inside.step & s.pos.any)
        //    gen.l << location(s.pos);
        
        CodeGen child = gen.child();
        gen.l << begin(child.block);

        Bool returns = s.code(child);

        gen.l << end(child.block);
        returns;
    }
}

class Block extends Stmt {
    Scope scope;
    Stmt[] stmts;
    protected Str->JavaVar vars;
    Bool step;

    init(SrcPos pos, Scope parent) {
        init(pos) { step = true; }
        scope = blockScope(this, parent);
    }

    init(SrcPos pos, Block parent) {
        init(pos) { step = parent.step; }
        scope = blockScope(this, parent.scope);
    }

    void add(Stmt stmt) {
        if (stmt !is this)
            stmts << stmt;
    }

    void add(JavaVar var) {
        if (vars.has(var.name))
            throw SyntaxError(pos, "Current block already has a variable named ${var.name}.");
        vars.put(var.name, var);
    }

    void initVariables(CodeGen gen) {
        for (k in vars) {
            vars.get(k.name).create(gen);
        }
    }

    Named? variable(Str name) {
        if (vars.has(name))
            return vars.get(name);
        else
            return null;
    }

    Bool code(CodeGen gen) : override {
        initVariables(gen);
        for (st in stmts) {
            gen.l << location(st.pos);
            st.code(gen);
        }
        true;
    }
}

private Scope blockScope(Block block, Scope parent) {
    if (t = parent.top) {
        parent.child(BlockLookup(block, t));
    }
    else {
        parent.child(BlockLookup(block));
    }
}

class BlockLookup extends NameLookup {
    init(Block block) {
        init() { block = block; }
    }

    init(Block block, NameLookup prev) {
        init(prev) {
            block = block;
        }
    }

    Block block;

    Named? find(SimplePart part, Scope source) {
        if (part.params.empty)
            return block.variable(part.name);
        
        null;
    }
}

class Return extends Expr {
    Block block;
    Expr? ret;
    
    init(SrcPos pos, Block block, Expr? ret) {
        init(pos) {
            block = block;
            ret = ret;
        }
    }

    init(SrcPos pos, Block block) {
        init(pos) {
            block = block;
        }
    }

    Bool code(CodeGen gen) : override {
        var fn = findFnRoot(block);
        if (ret) {
            if (fn.result == Value())
                throw SyntaxError(pos, "${fn} should not return a value, instead it returns ${ret.result}.");
            ret.code(gen);
            gen.l << fnRet(eax);
        } else if (fn.result != Value())
            throw SyntaxError(pos, "${fn} must return a value!");
        else {
            gen.l << fnRet();
        }
        true;
    }

    Value result() : override { expr.result; }

    Bool suggestResult(Type t) : override { false; }
}

Function findFnRoot(Block inside) on Compiler {
    findFnRoot(inside.scope);
}

Function findFnRoot(Scope in) on Compiler {
    NameLookup? at = in.top;
    while (curr = at) {
        if (curr as JavaFunction) {
            return curr;
        }
        at = curr.parent;
    }
    
    throw InternalError("Could not find function root.");
}